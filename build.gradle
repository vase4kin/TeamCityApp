/*
 * Copyright 2019 Andrey Tolpeev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

import teamcityapp.buildsrc.Libs
import teamcityapp.buildsrc.Config

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath Libs.Tools.gradleAndroid
        classpath Libs.Tools.gradleversions
        classpath Libs.Tools.androidAapt
        classpath Libs.Tools.googleServices
        classpath Libs.Tools.firebasePerf
        classpath Libs.Tools.jacoco
        classpath Libs.Tools.kotlinter
        classpath Libs.Tools.fabric
        classpath Libs.Kotlin.Tools.gradlePlugin
        classpath Libs.Kotlin.Tools.extensions

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://maven.fabric.io/public" }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {

            apply plugin: 'com.github.ben-manes.versions'
            apply plugin: 'org.jmailen.kotlinter'

            android {
                buildToolsVersion Config.buildTools
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    release {}
                    debug {
                        testCoverageEnabled true
                    }
                    showcase.initWith(buildTypes.release)
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                kotlinter {
                    ignoreFailures = false
                    indentSize = 4
                    continuationIndentSize = 4
                    reporters = ["html"]
                    experimentalRules = false
                    disabledRules = ["no-wildcard-imports", "import-ordering"]
                    fileBatchSize = 30
                }

                lintOptions {
                    disable 'InvalidPackage'
                    xmlReport false
                    abortOnError true
                    htmlOutput file("$buildDir/reports/lint-report/lint-report.html")
                }

                packagingOptions {
                    exclude 'LICENSE.txt'
                    //https://code.google.com/p/android/issues/detail?id=195331
                    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
                    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
                }

                testOptions {
                    unitTests {
                        all {
                            includeAndroidResources = true
                            jvmArgs '-noverify'
                            testLogging {
                                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                            }
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
