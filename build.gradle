/*
 * Copyright 2019 Andrey Tolpeev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath Deps.tools_gradleandroid
        classpath Deps.tools_kotlin
        classpath Deps.tools_gradleversions
        classpath Deps.tools_androidAapt
        classpath Deps.tools_googleServices
        classpath Deps.tools_jacoco
        classpath Deps.tools_firebasePerf
        classpath Deps.tools_kotlinter
        classpath Deps.tools_fabric

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://maven.fabric.io/public" }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"
            apply plugin: 'org.jmailen.kotlinter'
            apply from: 'scripts/jacoco.gradle'
            apply from: 'scripts/pmd.gradle'
            apply from: 'scripts/findbugs.gradle'

            android {
                buildToolsVersion Config.buildTools
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                kotlinter {
                    ignoreFailures = false
                    indentSize = 4
                    continuationIndentSize = 4
                    reporters = ["html"]
                    experimentalRules = false
                    disabledRules = ["no-wildcard-imports", "import-ordering"]
                    fileBatchSize = 30
                }

                lintOptions {
                    disable 'InvalidPackage'
                    xmlReport false
                    abortOnError true
                    htmlOutput file("build/reports/lint-report/lint-report.html")
                }

                packagingOptions {
                    exclude 'LICENSE.txt'
                    //https://code.google.com/p/android/issues/detail?id=195331
                    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
                    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
                }

                testOptions {
                    unitTests {
                        all {
                            jvmArgs '-noverify'
                            testLogging {
                                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                            }
                        }
                    }
                }
            }

            dependencies {
                implementation Deps.kotlin_stdlibJdk7
            }
        }
    }
}
