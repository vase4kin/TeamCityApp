/*
 * Copyright 2016 Andrey Tolpeev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply from: 'jacoco.gradle'
apply from: 'spoon.gradle'
apply from: 'pmd.gradle'
apply from: 'findbugs.gradle'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.github.vase4kin.teamcityapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 31
        versionName "1.2.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            minifyEnabled true
        }
        debug {
            testCoverageEnabled true
            applicationIdSuffix = ".debug"
        }
        showcase.initWith(buildTypes.release)
        showcase {
            applicationIdSuffix = ".showcase"
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
        if (variant.buildType.name.equals('showcase')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        //https://code.google.com/p/android/issues/detail?id=195331
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        xmlReport false
        abortOnError true
        htmlOutput file("build/reports/lint-report/lint-report.html")
    }

    dexOptions {
        jumboMode true
    }
}

buildscript {
    repositories {
        mavenCentral() // or jCenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.1.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.6'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories{

    mavenCentral()

    flatDir{
        dirs 'libs'
    }

    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Android support libraries
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v13:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:customtabs:$rootProject.supportLibraryVersion"
    // Api third party libraries
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    // Drawer
    compile('com.mikepenz:materialdrawer:4.6.3@aar') {
        transitive = true
    }
    // Icons
    compile 'com.joanzapata.iconify:android-iconify-material:2.2.2'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.2'
    compile 'com.joanzapata.iconify:android-iconify-material-community:2.2.2'
    // Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    // Views
    compile 'com.github.xiprox.errorview:library:2.2.0'
    compile 'com.pnikosis:materialish-progress:1.5'
    compile 'com.cocosw:bottomsheet:1.3.0@aar'
    // View injection
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    // Event bus
    compile 'org.greenrobot:eventbus:3.0.0'
    // Others
    compile 'joda-time:joda-time:2.8.1'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile 'com.vinaysshenoy:mugen:1.0.1'
    compile('com.mikepenz:aboutlibraries:5.7.3@aar') {
        transitive = true
    }
    // Onboarding
    compile 'uk.co.samuelwall:material-tap-target-prompt:1.9.5'
    // Dagger
    apt 'com.google.dagger:dagger-compiler:2.11'
    compile 'com.google.dagger:dagger:2.11'
    provided 'javax.annotation:jsr250-api:1.0'
    // Rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.3.0'
    // Rx cache
    compile "com.github.VictorAlbertos.RxCache:runtime:1.7.0-1.x"
    compile 'com.github.VictorAlbertos.Jolyglot:gson:0.0.3'
    // Encryption
    compile 'com.facebook.conceal:conceal:1.1.3@aar'
    // Rate app library
    compile 'com.github.hotchemi:android-rate:1.0.1'
    // Firebase
    compile 'com.google.firebase:firebase-core:10.2.0'
    // Unit tests
    testCompile "org.mockito:mockito-core:$rootProject.mockitoCoreVersion"
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile "org.powermock:powermock-module-junit4:$rootProject.powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule:$rootProject.powerMockVersion"
    testCompile "org.powermock:powermock-api-mockito:$rootProject.powerMockVersion"
    testCompile "org.powermock:powermock-classloading-xstream:$rootProject.powerMockVersion"
    // Ui tests
    androidTestCompile "com.android.support.test:runner:$rootProject.espressoRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.espressoRunnerVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-web:$rootProject.espressoVersion"
    androidTestCompile "org.mockito:mockito-core:$rootProject.mockitoCoreVersion"
    androidTestCompile "com.crittercism.dexmaker:dexmaker:$rootProject.dexMakerVersion"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:$rootProject.dexMakerVersion"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:$rootProject.dexMakerVersion"
    androidTestCompile 'com.squareup.spoon:spoon-client:1.5.4'
    androidTestCompile 'com.github.fabioCollini:DaggerMock:0.6.1'
    androidTestCompile 'com.jraska:falcon-spoon-compat:1.0.3'
    // Resolve conflits between apks
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
}

apply plugin: 'com.google.gms.google-services'
